# Copyright (c) 2019 Anton Semjonov
# Licensed under the MIT License

# metadata, labels, ranges, etc.
set title "Speedtest Results\ngithub.com/ansemjo/speedtest-plotter"
set timestamp "plotted %F %T %Z"
set xlabel "Measurement Date"
set xdata time
set timefmt "%Y-%m-%dT%H:%M:%S.*Z"
set xtics format "%Y-%m-%d\n%H:%M:%S" out
set ylabel "Measured Speed"
set ytics format "%.2s %cBit/s" nomirror out
set yrange [0:*]
set y2label "Ping"
set y2tics format "%.0f ms" nomirror out
set y2range [0:*]
set grid
set datafile separator ","

# line styles
set linetype 1 lw 1 lc rgb "skyblue"            # download
set linetype 2 lw 1 lc rgb "coral"              # upload
set linetype 3 lw 1 lc rgb "light-green" pt 3   # ping
set linetype 4 lw 3 lc rgb "navy"               # smooth download
set linetype 5 lw 3 lc rgb "orange-red"         # smooth upload

# calculate averages per dimension
set fit quiet logfile "/dev/null"
avgping(x) = ping
fit avgping(x) ARG1 using 4:6 via ping
avgdown(x) = down
fit avgdown(x) ARG1 using 4:7 via down
avgup(x) = up
fit avgup(x) ARG1 using 4:8 via up

# plot the results
set terminal svg size 1280, 720 dynamic
set output ARG2
plot \
  for [col=7:8] ARG1 using 4:col notitle with steps, \
  ARG1 using 4:6 title gprintf("Ping (≈ %.0f ms)", avgping(x)) axes x1y2 with points, \
  ARG1 using 4:7 title gprintf("Download (≈ %.2s %cBit/s)", avgdown(x)) sm bezier, \
  ARG1 using 4:8 title gprintf("Upload (≈ %.2s %cBit/s)", avgup(x)) sm bezier, \
