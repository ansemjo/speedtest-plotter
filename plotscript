# Copyright (c) 2019 Anton Semjonov
# Licensed under the MIT License

set title "Speedtest Results\ngithub.com/ansemjo/speedtest-plotter"
set timestamp
set xlabel "Measurement Date [UTC]"
set xdata time
set timefmt "%Y-%m-%dT%H:%M:%S.*Z"
set xtics format "%Y-%m-%d\n%H:%M:%S" rotate by -40
set ylabel "Measured Speed"
set ytics format "%.2s %cBit/s" nomirror
set yrange [0:*]
set linetype 1 lw 3      #lc rgb "blue"
set linetype 2 lw 3      #lc rgb "red"
set linetype 3 lw 1 pt 9 #lc rgb "green"
set y2label "Ping"
set y2tics format "%.0f ms" nomirror
set y2range [0:100]
set grid
set datafile separator ","
set terminal png size 1280, 720
set output ARG2
plot for [col=7:8] ARG1 using 4:col title col with histeps, \
  '' using 4:6 title col axes x1y2 with points
